// Synchronous Primary/Backup Replication.
// "run-main edu.berkeley.cs.boom.molly.SyncFTChecker --EOT 6 --EFF 4 --crashes 1 --prov-diagrams --negative-support --nodes C,a,b,c src/test/resources/examples_ft/nemo/pb_synchronous.ded"


// Network relationships.

network("a", "a")@1;
network("a", "b")@1;
network("a", "c")@1;
network("b", "a")@1;
network("b", "b")@1;
network("b", "c")@1;
network("c", "a")@1;
network("c", "b")@1;
network("c", "c")@1;
network(Node, Other)@next :- network(Node, Other);

primary("a", "a")@1;
primary(Node, Prim)@next :- primary(Node, Prim);

replica("a", "b")@1;
replica("a", "c")@1;
replica(Prim, Rep)@next :- replica(Prim, Rep);

conn_out("C", "a")@1;
conn_out("a", "C")@1;
conn_out(Cli, Prim)@next :- conn_out(Cli, Prim);


// System state tables (counting rows).
num_log(Prim, Pload, count<Node>) :- primary(Prim, Prim), log(Node, Pload);
num_uncrashed(Prim, count<Node>) :- primary(Prim, Prim), network(Prim, Node), notin crash(Prim, Node, _);
num_replicate_log(Prim, Pload, count<Rep>) :- primary(Prim, Prim), replicate_log(Prim, Rep, Pload, _);
num_replicate_acked(Prim, Pload, count<Rep>) :- primary(Prim, Prim), replicate_acked(Prim, Rep, Pload);


// Trigger start.
begin("C", "foo")@1;


// Protocol behavior.

// Client sends request to primary.
request(Prim, Pload, Cli)@async :- begin(Cli, Pload), conn_out(Cli, Prim);


// Primary logs the replication and replicates to replicas.
replicate_log(Prim, Rep, Pload, Cli) :- request(Prim, Pload, Cli), primary(Prim, Prim), replica(Prim, Rep);
replicate_log(Prim, Rep, Pload, Cli)@next :- replicate_log(Prim, Rep, Pload, Cli);

// Primary acks to client after having seen all replicas acknowledging. Synchronous Primary/Backup.
ack(Cli, Prim, Pload)@async :- conn_out(Prim, Cli), num_replicate_log(Prim, Pload, CntLogged), num_replicate_acked(Prim, Pload, CntAcked), CntLogged == CntAcked, notin acked(Cli, Prim, Pload);
acked(Cli, Prim, Pload) :- ack(Cli, Prim, Pload);
acked(Cli, Prim, Pload)@next :- acked(Cli, Prim, Pload);

// Primary replicates to replicas.
replicate(Rep, Pload, Prim, Cli)@async :- request(Prim, Pload, Cli), replicate_log(Prim, Rep, Pload, Cli);

// Nodes in the network persist received requests.
log(Prim, Pload) :- request(Prim, Pload, Cli);
log(Rep, Pload) :- replicate(Rep, Pload, _, _);
log(Rep, Pload)@next :- log(Rep, Pload);

// Ack from replicas to primary.
replicate_ack(Prim, Rep, Pload, Cli)@async :- log(Rep, Pload), replicate(Rep, Pload, Prim, Cli);
replicate_acked(Prim, Rep, Pload) :- replicate_ack(Prim, Rep, Pload, _);
replicate_acked(Prim, Rep, Pload)@next :- replicate_acked(Prim, Rep, Pload);


// Track durability state.
persisted_log(Prim, Pload) :- primary(Prim, Prim), num_log(Prim, Pload, CntLog), num_uncrashed(Prim, CntCorrect), CntLog > CntCorrect;
persisted_log(Prim, Pload) :- primary(Prim, Prim), num_log(Prim, Pload, CntLog), num_uncrashed(Prim, CntCorrect), CntLog == CntCorrect;


// Invariants.

// E2E property: Any time we ACK a payload to a client, we expect
// that payload to be persisted on all correct replicas.
pre(Pload) :- acked(Cli, Prim, Pload);
post(Pload) :- primary(Prim, Prim), persisted_log(Prim, Pload);
