// "run-main edu.berkeley.cs.boom.molly.SyncFTChecker --EOT 8 --EFF 4 --crashes 0 --prov-diagrams --negative-support --nodes C,a,b,c src/test/resources/examples_ft/nemo/elastic_2.ded"


member(N, O)@next :- member(N, O);

primary(N, P)@next :- primary(N, P), notin view_change(N, _);
primary(N, P)@next :- view_change(N, P);

write_msg(P, W)@async :- client_write(C, W), primary(C, P);

seq(N, V)@next :- seq(N, V), notin write_msg(N, _), notin replicate(N, _, _);
seq(N, V + 1)@next :- seq(N, V), write_msg(N, _);
seq(N, V + 1)@next :- replicate(N, _, V);

update_primary(P, N, W, S) :- write_msg(P, W), seq(P, S), member(P, N), P != N;
replicate(N, W, S)@async :- update_primary(P, N, W, S);
log(P, W, S)@next :- update_primary(P, N, W, S);

// the buggy logic
update_replica(N, W, S) :- replicate(N, W, S), notin log(N, _, S);
// fixed?
// update_replica(N, W, S) :- replicate(N, W, S), notin log(N, W, S);

log(N, W, S)@next :- log(N, W, S), notin update_replica(N, _, S);
log(N, W, S)@next :- update_replica(N, W, S);


num_same_log(N, count<W>) :- log(N, W, S), log(M, W, S), N != M;
num_same(N, count<M>) :- num_same_log(M, _), member(N, M);
num_nodes(N, count<M>) :- member(N, M);

pre(W, S) :- log(_, W, S);
post(W, S) :- log(N, W, S), num_same(N, CntSame), num_nodes(N, CntNodes), CntSame == CntNodes;


member("a", "a")@1;
member("a", "b")@1;
member("a", "c")@1;
member("b", "a")@1;
member("b", "b")@1;
member("b", "c")@1;
member("c", "a")@1;
member("c", "b")@1;
member("c", "c")@1;

view_change("a", "a")@1;
view_change("C", "a")@1;

client_write("C", "hi!")@2;

view_change("b", "b")@3;
view_change("C", "b")@3;

client_write("C", "oh no")@4;

seq("a", 0)@1;
seq("b", 0)@1;
